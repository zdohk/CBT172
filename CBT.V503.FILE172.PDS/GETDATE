         TITLE 'GETDATE - GETS THE DATE, AND SO ON SO FORTH...'
***********************************************************************
*                                                                     *
*        PDSDOC *USAF PROGRAM TO DO DATE CONVERSION + HOLIDAY TABLE...* GETDATE
*                                                                     *
* 15SEP00 DHC UPDATED FOR 2001 AT AGCO.                               *
* 28JUN00 DHC UPDATED FOR Y2K AT AGCO.                                *
* 10JAN92 CART1 COPIED FROM CBT321.FILE316, CHANGE HOLIDAY TABLE.     *
*                                                                     *
***********************************************************************
         SPACE 1
*$DOC@----------------------------------------------------------------*
*
*                   G  E  T  D  A  T  E
*
*        GETDATE  OBTAINS THE DATE AND DAY OF WEEK AND IF IT IS A
*                 A HOLIDAY FOR OTHER PROGRAMS
*
*        WHEN THE ROUTINE IS CALLED, R1 MUST CONTAIN THE ADDRESS
*        OF A LIST OF ONE OR TWO ADDRESS, ALIGNED ON A FULLWORD
*        BOUNDRY, WITH A X'80' IN THE HIGH-ORDER BYTE POSITION
*        OF THE LAST ADDRESS. THE FIRST ADDRESS SHOULD REFERENCE
*        A 9-BYTE FIELD AND THE SECOND ADDRESS A 10-BYTE FIELD.
*        IF ONE ADDRESS IS PASSED, 'GETDATE' WILL RETURN THE DATE
*        IN THAT LOCATION (IN THE FORM OF CL9'DD MMM YY'). THE
*        ORIGINAL CONTENTS OF THAT LOCATION DICTATE WHAT DATE
*        WILL BE RETURNED. IF THAT LOCATION CONTAINS
*        A JULIAN DATE (IN THE FORM  PL4'0CYYDDD'), THAT DATE
*        IS RETURNED. IF A SECOND ADDRESS IS PASSED, THE CORRES-
*        PONDING DAY OF THE WEEK WILL BE RETURNED IN THAT LOCATION
*        (IN THE FORM CL9'NNNNNNNN', PADDED ON THE RIGHT WITH
*        BLANKS, IF NECESSAY). ALSO IN THE 10TH BYTE OF THE FIELD
*        WILL EITHER BE A BLANK (X'40') FOR THE DAY BEING A WORK
*        DAY OR A H (C'H') HOLIDAY.
*
*
*        AS AN EXAMPLE, THE NECESSARY LINKAGE WILL BE SIMULATED.
*        R1 WILL BE LOADED, AND THE DATE ROUTINE WILL BE CALLED.
*
*            LA    R1,LIST          LOAD ADDR OF ADDR LIST INTO R1
*            LINK  EP=GETDATE       LINK TO GETDATE
*            .
*            .
*            .
*   LIST     DC    A(DATE)
*            DC    X'80',AL3(DAY)
*   DATE     DS    CL9
*   DAY      DS    0CL10
*            DS    CL9
*   HOLIDAY  DS    CL1
*
*            NOTES:  1. THERE IS A TABLE OF HOLIDAYS. ADD YOUR OWN
*                       HOLIDAYS INTO THE TABLE.
*                    2. GETDATE IS REENTRANT.
*                    3. HAVE FUN  !!!!
*
*                              JIM MARSHALL, CAPT, USAF
*                              (301) 688-6829
*
* INSTALLED:  5 JAN 84
*
* CHANGE LOG: 20NOV84 - RANDOM S0C7'S WHEN YOU LIKE TO 'GETDATE' FROM
*             ANOTHER PROGRAM. GETMAIN'D AREA WAS NOT INIT.      JDM1
*             04OCT00 PARMAMETER LIST MODIFIED FOR Y2K           DHC1
*$DOC$----------------------------------------------------------------*
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE
GETDATE  CSECT
         SAVE  (14,12)
         LR    R12,R15
         USING GETDATE,R12
         LR    R9,R1                   SAVE REG 1 FOR LATER
         L     R0,WORKSIZE             GET SUBPOOL NO & LENGTH
         SPACE
         GETMAIN R,LV=(0)              GO GET STORAGE
         USING WORKAREA,R1             GET ADDRESSABILITY (TEMP)
         SPACE
         XC    SAVEAREA,SAVEAREA       CLEAR SAVE AREA
         ST    R13,SAVEAREA+4
         ST    R1,8(R13)
         LR    R13,R1
         DROP  R1
         SPACE
         USING WORKAREA,R13            GET ADDRESSABILITY
         XR    R2,R2                         CLEAR R2
         LR    R1,R9                   RESTORE R1
         L     R3,0(R1)                GET ADDR OF PARM 1
         CLI   0(R1),X'80'             IS THIS THE ONLY PARM?
         BE    GO                      IF SO, SKIP NEXT PART
         L     R2,4(R1)                ELSE, GET ITS ADDR
         SPACE
GO       EQU   *
         CLI   0(R3),X'40'             CHARACTER FIELD?         DHC1
         BL    REVERSE                 NO, A JULIAN DATE        DHC1
         TIME  DEC                     GET THE DATE
         ST    R1,DATETIME             AND, SAVE IT
         B     SKIP                    SKIP NEXT PART
         SPACE
REVERSE  EQU   *
         MVC   DATETIME(4),0(R3)       GET PASSED DATE
         SPACE
SKIP     EQU   *
         MVC   YEAR(8),=D'0'           INITIALIZE                JDM1
         MVI   2(R3),C' '              INITIALIZE...
         MVI   6(R3),C' '              BLANKS
         MVI   YEAR+7,X'0F'            SET UP YEAR...
         MVO   YEAR+5(3),DATETIME(2)   FOR UNPACKING
         UNPK  7(2,R3),YEAR+6(2)       SAVE YEAR
         CVB   R5,YEAR                 CONVERT YEAR TO BINARY
         STC   R5,SAVEYEAR             SAVE THAT VALUE
         LTR   R2,R2                   TEST R2 FOR 2ND PARM
         BZ    SKIPTHIS                IF ZERO, BRANCH; ELSE...
         MVC   YEAR+6(2),DATETIME+2    GET DATE
         CVB   R7,YEAR                 CONVERT TO BINARY
         M     R4,=F'36525'            CALCULATE NUMBER...
         S     R5,=F'25'               OF DAYS SINCE...
         D     R4,=F'100'              SUNDAY...
         AR    R5,R7                   DEC. 31,1899...
         XR    R4,R4                   AND...
         D     R4,=F'7'                MOD BY 7...
         SRDL  R4,32                   AND...
         M     R4,=F'9'                CALCULATE INDEX...
         LA    R5,SMTWTFS(R5)          AND POINTER TO DAY TABLE
         MVC   0(9,R2),0(R5)           MOVE CORRECT DAY TO PARM2
SKIPTHIS EQU   *
         LA    R7,DAYS-3               POINTER TO MONTHS
         TM    SAVEYEAR,X'03'          TEST FOR LEAP YEAR
         BZ    LEAPYEAR                BRANCH FOR LEAP YEAR
         LA    R8,DAYS-7               LOAD ADDR OF DAYS
         B     NXTMONTH                SKIP THIS PART
LEAPYEAR EQU   *
         LA    R8,DAYS-5               LOAD ADDR FOR LEAP YEAR
NXTMONTH EQU   *
         LA    R8,7(R8)                BUMP ADDR OF DAY
         LA    R7,7(R7)                INCREMENT MONTH POINTER
         CP    DATETIME+2(2),0(2,R8)   COMPARE DATE W/DAYS
         BNH   NXTMONTH                BRANCH LOW OR EQUAL
         SP    DATETIME+2(2),0(2,R8)   OTHERWISE SUBTRACT DAYS
         OI    DATETIME+3,X'0F'        UNPACK DAYS
         UNPK  0(2,R3),DATETIME+2(2)   SAVE DAY
         TM    0(R3),X'0F'             FIRST DIGIT OF DATE ZERO?
         BM    CONTINUE                IF NOT, CONTINUE
         MVI   0(R3),X'40'             ELSE, BLANK OUT THAT DIGIT
CONTINUE MVC   3(3,R3),0(R7)           SAVE MONTH
         SPACE
*---------------------------------------------------------------------*
*        THIS PART OF THE CODE WILL DETERMINE IF THE DAY CHOSEN
*        IS CONSIDERED A HOLIDAY. IF SO THEN AN INDICATOR WILL
*        PLACED IN THE LAST DATA AREA OF THE PARAMETER LIST.
*---------------------------------------------------------------------*
         SPACE
         LTR   R2,R2                   IS HOLDIAY TEST DESIRED?
         BZ    RETURN
         MVI   9(R2),C' '              ASSUME NO
         LA    R10,HOLIDAYS            GET THE ADDR OF THE LIST
LOOP     EQU   *
         CLI   0(R10),X'FF'            END OF LIST ???
         BE    RETURN                  WENT THROUGH IT
         CLC   0(9,R3),0(R10)          DOES IT MATCH ???
         BE    STORHOL                 PUT IN THE FACT
         LA    R10,9(R10)              BUMP THE ADDRESS
         B     LOOP
STORHOL  EQU   *
         MVI   9(R2),C'H'
         B     RETURN
         SPACE
*-------------------------------------------------------------------*
*        RETURN TO THE CALLER. FIRST WE MUST FREE THE WORKAREA
*        AND THEN RETURN TO THE CALLER. INSURE WE RETURN WITH
*        INDICATOR SET FOR FORTRAN USERS.
*-------------------------------------------------------------------*
         SPACE
RETURN   EQU  *
         LR   R1,R13
         L    R4,4(R13)
         LA   R15,0
         ST   R15,16(R4)
         L    R0,WORKSIZE
         L    R13,4(R13)
         FREEMAIN R,LV=(0),A=(1)
         LM   R14,R12,12(R13)
         BR   R14
*-------------------------------------------------------------------*
*        PUT ALL OUR CONSTANTS THE THIS AREA
*-------------------------------------------------------------------*
         SPACE
         DS   0F
WORKSIZE DC   AL1(000),AL3(WORKEND-WORKAREA)
DAYS     DC    PL2'334',PL2'335'        NOV
         DC    CL3'DEC'
         DC    PL2'304',PL2'305'        OCT
         DC    CL3'NOV'
         DC    PL2'273',PL2'274'        SEP
         DC    CL3'OCT'
         DC    PL2'243',PL2'244'        AUG
         DC    CL3'SEP'
         DC    PL2'212',PL2'213'        JUL
         DC    CL3'AUG'
         DC    PL2'181',PL2'182'        JUN
         DC    CL3'JUL'
         DC    PL2'151',PL2'152'        MAY
         DC    CL3'JUN'
         DC    PL2'120',PL2'121'        APR
         DC    CL3'MAY'
         DC    PL2'090',PL2'091'        MAR
         DC    CL3'APR'
         DC    PL2'059',PL2'060'        FEB
         DC    CL3'MAR'
         DC    PL2'031',PL2'031'        JAN
         DC    CL3'FEB'
         DC    PL2'000',PL2'000'
         DC    CL3'JAN'
SMTWTFS  DC    CL9'SUNDAY'
         DC    CL9'MONDAY'
         DC    CL9'TUESDAY'
         DC    CL9'WEDNESDAY'
         DC    CL9'THURSDAY'
         DC    CL9'FRIDAY'
         DC    CL9'SATURDAY'
         SPACE
*--------------------------------------------------------------------*
*        THESE ARE THE AGCO HOLIDAYS.                                *
*--------------------------------------------------------------------*
         SPACE
HOLIDAYS DC    C' 3 JAN 00'        NEW YEARS DAY
         DC    C'24 APR 00'        EASTER MONDAY
         DC    C'25 APR 00'        EASTER TUESDAY
         DC    C'29 MAY 00'        WHITSUN SPRING HOLIDAY
         DC    C'30 MAY 00'        WHITSUN SPRING HOLIDAY
         DC    C'28 AUG 00'        AUGUST BANK HOLIDAY
         DC    C'25 DEC 00'        CHRISTMAS
         DC    C'26 DEC 00'        BOXING DAY
         DC    C' 1 JAN 01'        NEW YEARS DAY
         DC    C'16 APR 01'        EASTER MONDAY
         DC    C'17 APR 01'        EASTER TUESDAY
         DC    C'28 MAY 01'        WHITSUN SPRING HOLIDAY
         DC    C'29 MAY 01'        WHITSUN SPRING HOLIDAY
         DC    C'27 AUG 01'        AUGUST BANK HOLIDAY
         DC    C'25 DEC 01'        CHRISTMAS
         DC    C'26 DEC 01'        BOXING DAY
         DC    C' 1 JAN 02'        NEW YEARS DAY
         DC    X'FF'               END OF THE LIST !!!!!!
         LTORG
         EJECT
*--------------------------------------------------------------------*
*        ALL THE DSECTS WILL FOLLOW
*--------------------------------------------------------------------*
         SPACE
WORKAREA DSECT
         DS   0D
SAVEAREA DS   18F
YEAR     DS   D
DATETIME DS   F
SAVEYEAR DS   C
SWDAY    DS   C
SWHOLDAY DS   C
WORKEND  EQU  *
         END
