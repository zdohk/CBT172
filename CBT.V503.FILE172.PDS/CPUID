         TITLE ' CPUID- CHANGES THE CPUID IN PCCAS'                     SPG00010
*********************************************************************** SPG00020
*                                                                       SPG00030
*  WRITTEN  APR. 9/93 BY RSW                                            SPG00040
*  ---------                                                            SPG00050
*  06OCT00 DHC MODIFIED WITH MY MACROS AND PARM PROCESSING              SPG00090
*                                                                       SPG00090
*                                                                       SPG00060
*  TO CHG THE CPUID ON THE DISASTER RECOVERY SYSTEM TO THAT OF THE      SPG00070
*  MAIN SYSTEM IN ALL THE PCCA'S. EG IN A 5 ENGINE MACHINE THERE WILL   SPG00080
*  BE 5 PCCA'S                                                          SPG00090
*                                                                       SPG00090
*   TO USE THIS PGM, FIRST, CODE A PROC IN SYS1.PROCLIB                 SPG00090
*                                                                       SPG00090
* //CHGCPUID  PROC                                                      SPG00090
* //   EXEC PGM=CPUID,PARM='NNNNTTTT'                                   SPG00090
*                                                                       SPG00090
*          THE PARM IS UUP TO EIGHT DIGITS AS FOLLOWS;                  SPG00090
*                                                                       SPG00090
*           NNNN = LAST FOUR CHARACTERS OF THE DESIRED CPUID            SPG00090
*           TTTT = CPU TYPE E.G. 9672 (OPTIONAL)                        SPG00090
*                                                                       SPG00090
*                                                                       SPG00090
* NEXT, ASSEMBLE AND LINK INTO AN AUTHORIZED, LINKLISTED LIB            SPG00090
* WHICH WILL ALWAYS BE ON TEST SYSTEM  (SUCH AS SYS1.CMDLIB)            SPG00090
*                                                                       SPG00090
* IN SYS1.PARMLIB MEMBER(IEACMD00) AFTER S LLA STMT                     SPG00090
*  CODE  START CHGCPUID,SUB=MSTR                                        SPG00090
*
*  FROM A SYSTEM CONSOLE, AFTER CHGCPUID ENDS, ISSUE COMMAND
*  D M=CPU   AND NOTE THAT ALL CPUIDS SHOW 9672 AS MODEL
*
*********************************************************************** SPG01350
*                                                                       SPG01360
CPUID    CSECT 0                                                        SPG01370
CPUID    AMODE 31                                                       SPG01370
CPUID    RMODE 24                                                       SPG01370
         PGMIN EQUATES=YES,USING=(R13) STANDARD HOUSEKEEPING
         GETAUTH TYPE=GET          ENSURE WE ARE AUTHORISED
         GOSUB PARMPROC            TEST PARM VALUE
         LTR   R15,R15             WAS PARM VALID?
         BNZ   ERROR               NO, GIVE UP
         MODESET KEY=ZERO          * KEY ZERO                           SPG03340
         L     R3,16               * ADDRESS CVT
         USING CVT,R3
         L     R2,CVTPCCAT         * --> PCCA VECTOR TABLE (CVT+2FC)
         USING PCCAVT,R2
         LA    R6,PCCAT00P         * --> PCCA FOR CPU 0
         DROP  R2
         LA    R7,16               * LOOP CTR ( MAX OF 16 CPUS)
         L     R1,PARMADDR         REFRESH PARM ADDRESS
         L     R2,PARMLGTH         GET PARM LENGTH
         BCTR  R2,0                DECREMENT FOR EXECUTE
         SPACE 1
LOOP     DS    0H
         ICM   R3,15,0(R6)         * --> PCCA
         BNZ   TESTIT              * THERE IS ONE
BUMP     DS    0H
         LA    R6,4(R6)            * TO NEXT PCCA ADDRESS
         BCT   R7,LOOP             * LOOP TILL DONE
         B     FINISHED            * THEN QUIT
*
TESTIT   DS    0H
         USING PCCA,R3
         CLC   PCCAPCCA,=CL4'PCCA' * MAKE SURE IT IS PCCA
         BNE   ERROR               * PROBLEM SOMEWHERE???
*        MVC   PCCACPID+8(4),=C'9672'     * NEW MODEL NO.
*        MVC   PCCACPID+4(8),=C'70249021' * NEW CPUID
         LA    R4,PCCACPID+4       POINT PAST CP & LPAR NO.
         EX    R2,PARMMOVE         COPY PARM TO CPUID
         B     BUMP                * LOOP TILL DONE
         DROP  R3
*
FINISHED DS    0H
         WTO   'CPU IDS HAVE BEEN CHANGED '
         B     EXIT
*
ERROR    DS    0H
         WTO   'ERROR IN PGM CPUID'
*
EXIT     DS    0H
         MODESET KEY=NZERO         USER STATUS                          SPG03380
         PGMOUT ,                  RETURN TO CALLER
         EJECT ,
*******************************************************************
*        PROCESS PARM FIELD IF SUPPLIED                           *
*******************************************************************
         SPACE 1
PARMPROC DS    0H                  PROCESS PARM FIELD
         SUBIN SAVE=(R4)           SUBROUTINE ENTRY
         SR    R15,R15             ASSUME PARM IS GOOD
         L     R1,0(R1)            LOAD ADDRESS OF PARM INTO R1
         ST    R1,PARMADDR         STORE ADDRESS OF PARM INFO'S
         LH    R2,0(R1)            GET ADDRESS OF PARM INFO'S
         ST    R2,PARMLGTH         SAVE PARM LENGTH
         LTR   R2,R2               TEST FOR A PARM
         BZ    PARMBAD             NONE, ERROR
         C     R2,=A(8)            TEST MAXIMUM LENGTH
         BH    PARMBAD             TOO LONG, ERROR
         BCTR  R2,0                DECREMENT FOR EXECUTE
         SPACE 1
PARMLOOP DS    0H                  TEST PARM VALUES
         EX    R2,PARMCLC          TEST FOR NUMERIC
         BH    PARMDONE            OK, USE IT
*        B     PARMBAD             NO, BAD VALUE
         SPACE 1
PARMBAD  DS    0H                  PARM FIELD INVALID
         LA    R15,8               REMEMBER WE FAILED
PARMDONE DS    0H                  PARM FIELD PROCESSED
         SUBOUT RESTORE=(R4)       RETURN TO CALLER
         TITLE 'DATA AREAS'
PARMCLC  CLC   2(*-*,R1),=CL8'00000000' TEST FOR NUMERIC   *EXECUTED*
PARMMOVE MVC   0(*-*,R4),2(R1)     MOVE PARM TO PCCACPID   *EXECUTED*
         SPACE 1
PARMADDR DC    A(0)                ADDRESS OF PARM FIELD
PARMLGTH DC    A(0)                LENGTH OF PARM FIELD
         LTORG ,
*                                                                       SPG07150
         CVT   LIST=NO,DSECT=YES
         IHAPCCAT DSECT=YES
         IHAPCCA  DSECT=YES
         END
