ASTRA    TITLE 'AGCO STARTED TASK RESOURCE AVAILABILITY'
***********************************************************************
*        PDSDOC *PROGRAM TO TEST FOR NAMED ADDRESS SPACES.............*
*                                                                     *
*        PROGRAM ID     :          ASTRA                              *
*                                                                     *
*        AUTHOR         :          D.H.CARTWRIGHT                     *
*        COPYRIGHT                 AGCO UK LTD.   2002                *
*                                                                     *
*        LOCATION       :          COVENTRY UK  CV4 9GF               *
*                                                                     *
*        HISTORY        :                                             *
*                                                                     *
* 08MAR02 DHC MODIFY 'LASSOO' FOR TSSO TO CHECK FOR RUNNING TASKS     *
*         WITHOUT CLUTTERING UP THE CONSOLE LOG WITH D J,XXXXX        *
*                                                                     *
*                                                                     *
*                                                                     *
***********************************************************************
         SPACE 2
***********************************************************************
*                                                                     *
*        PURPOSE                                                      *
*        -------                                                      *
*                                                                     *
* THIS PROGRAM WAS WRITTEN TO ASSIST CONSOLE AUTOMATION PACKAGES SUCH *
* AS TSSO. IT TELLS SUCH A PACKAGE WHETHER A NAMED TASK IS PRESENT IN *
* THE SYSTEM WITHOUT PARSING THE OUTPUT FROM 'D J,NAME'               *
* THE RETURN CODE INDICATES THE STATUS OF THE NAMED ADDRESS SPACE.    *
*                                                                     *
***********************************************************************
         EJECT ,
***********************************************************************
*                                                                     *
*        LIMITATIONS                                                  *
*        -----------                                                  *
*                                                                     *
* 1) MUST RUN AUTHORISED FROM AN AUTHORISED LIBRARY                   *
*                                                                     *
* 2) RUNS IN 24 BIT ADDRESSING MODE                                   *
*                                                                     *
***********************************************************************
         SPACE 1
***********************************************************************
*                                                                     *
*        INPUT                                                        *
*        -----                                                        *
*                                                                     *
* 1) THE PARM IS THE ONLY INPUT.                                      *
*    THE FORMAT IS 'JOBNAME' TO TEST FOR THE EXISTENCE OF 'JOBNAME'   *
*                                                                     *
***********************************************************************
         SPACE 1
***********************************************************************
*                                                                     *
*        OUTPUT                                                       *
*        ------                                                       *
*                                                                     *
* 1) THE RETURN CODE IS THE ONLY EXTERNAL OUTPUT FROM THE PROGRAM     *
*        0     TASK EXISTS AND IS SWAPPED IN                          *
*        4     TASK EXISTS BUT IS SWAPPED OUT                         *
*        8     JOBNAME NOT FOUND                                      *
*       16     NO JOBNAME SUPPLIED                                    *
*                                                                     *
***********************************************************************
         EJECT ,
         PRINT ON,GEN              SHOW MACRO EXPANSIONS
*        PRINT ON,NOGEN            SUPPRESS MACRO EXPANSIONS
         SPACE 1
ASTRA    CSECT ,
ASTRA    AMODE 31                  EXTENDED ADDRESSING
ASTRA    RMODE ANY                 ANY RESIDENCY MODE
         SPACE 1
***********************************************************************
*        REGISTER EQUATES AND USAGES                                  *
***********************************************************************
         SPACE 1
R0       EQU   0
R1       EQU   1                   PARAMETER PASSING
R2       EQU   2                   WORK
R3       EQU   3                   CVT POINTER
R4       EQU   4                   SUB-ROUTINE LINKAGE
R5       EQU   5                   PARM POINTER
R6       EQU   6                   LENGTH REGISTER
R7       EQU   7                   ASCB POINTER
R8       EQU   8                   OUCB POINTER
R9       EQU   9                   WORK
R10      EQU   10                  WORK
R11      EQU   11                  WORK
R12      EQU   12                  WORK
R13      EQU   13                  SAVE AREA POINTER AND BASE 1
R14      EQU   14                  SYSTEM LINKAGE RETURN
R15      EQU   15                  SYSTEM LINKAGE AND RETURN CODE
         SPACE 1
         ZCSECT GETMAIN=1,WKLEN=LWORK,REG=NO HOUSEKEEPING
         USING CLOSET,R13          ESTABLISH WORK AREA ADDSESSABILITY
         MVC   RETCODE,=H'0'       DEFAULT GOOD RETURN
         EJECT
***********************************************************************
*        PROGRAM MAIN LINE                                            *
***********************************************************************
         SPACE 1
MAINLINE DS    0H                  PROGRAM FUNCTION
         L     R5,0(,R1)           GET ADDRESS OF PARM FIELD
         BAL   R4,PARMPROC         GO PROCESS THE PARM
         GETAUTH TYPE=GET          ENSURE WE ARE AUTHORISED
         MODESET MODE=SUP,KEY=ZERO GET INTO SUPERVISOR MODE, PK0
         SETLOCK OBTAIN,TYPE=DISP,MODE=UNCOND,REGS=USE,                X
               RELATED=('ENSURE ASCB CHAIN DOES NOT CHANGE') LOCK
         BAL   R4,ASCBSCAN         GO FIND THE ASCB
         USING ASCB,R7             ESTABLISH ASCB ADDRESSABILITY
         L     R8,ASCBOUCB         PICK UP OUCB POINTER
         USING OUCB,R8             ESTABLISH OUCB ADDRESSABILITY
         SPACE 1
         TM    OUCBQFL,OUCBOUT     IS JOB SWAPPED OUT?
         BO    ERROR4              YES, TELL REQUESTOR
         SR    R9,R9               NO, SET RC=0
         SPACE 1
         DROP  R7                  END ASCB ADDRESSABILITY
         DROP  R8                  END OUCB ADDRESSABILITY
         EJECT ,
***********************************************************************
*        PROGRAM TERMINATION                                          *
***********************************************************************
         SPACE 1
GOODEND  DS    0H                  PARM WAS AT LEAST VALID
         SETLOCK RELEASE,TYPE=DISP,REGS=USE,                           X
               RELATED=('FREE LOCK AFTER SCAN OF ASCB CHAIN') UNLOCK
         MODESET MODE=PROB,KEY=NZERO RETURN TO OUR OWN PK
         SPACE 2
PGMEND   DS    0H                  PROGRAM CAME TO SOME END
         LH    R15,RETCODE         COPY RETURN CODE
         ZRETURN RC=(15)           EXIT PROGRAM
         TITLE 'FUNCTIONAL SUB-ROUTINES'
***********************************************************************
*        PARM PROCESSING ROUTINE                                      *
***********************************************************************
         SPACE 1
PARMPROC DS    0H                  PROCESS PARM FIELD
         LH    R6,0(,R5)           PICK UP PARM LENGTH
         LA    R5,2(,R5)           UPDATE PARM POINTER
         LTR   R6,R6               TEST FOR PARMS
         BZ    ERROR16             EXIT IF NONE
         CH    R6,=H'9'            TEST FOR EXCESS LENGTH
         BH    ERROR16             TOO LONG, BLOW UP
         SPACE 1
REQJNAME DS    0H                  ISOLATE JOBNAME
         MVC   JOBNAME,=CL8' '     SET JOBNAME TO BLANKS
         BCTR  R6,0                REDUCE PARM  FOR EXECUTE
         EX    R6,MOVEJOB          COPY THE JOBNAME FROM PARM
         SPACE 1
         BR    R4                  RETURN TO CALLER
         SPACE 1
MOVEJOB  MVC   JOBNAME(*-*),0(R5)  MOVE JOBNAME              *EXECUTED*
         EJECT ,
***********************************************************************
*        ROUTINE TO FIND REQUIRED ASCB                                *
*        ON EXIT R7 --> ASCB FOR 'JOBNAME'                            *
***********************************************************************
         SPACE 1
ASCBSCAN DS    0H                  RUN THE CHAIN OF ASCB'S
         L     R3,CVTPTR           GET CVT POINTER
         USING CVT,R3              ESTABLISH CVT ADDRESSABILITY
         ICM   R7,B'1111',CVTASCBH LOAD ADDRESS OF TOP ASCB
         USING ASCB,R7             ESTABLISH ASCB ADDRESSABILITY
         B     *+4+4               SKIP FIRST LOAD OF LOOP
         SPACE 1
ASCBLOOP DS    0H                  LOOP THROUGH ASCB CHAIN
         ICM   R7,B'1111',ASCBFWDP PICK UP FORWARD POINTER
         BZ    ERROR8              JOB NOT FOUND IF END
         ICM   R1,B'1111',ASCBJBNI GET ADDRESS OF JOBNAME INDICATOR
         BNZ   ASCBTEST            GOT IT, GO SEE IF WANTED
         ICM   R1,B'1111',ASCBJBNS GET ADDRESS OF STC NAME
         BZ    ASCBLOOP            TRY NEXT ASCB IF THIS ONE INVALID
ASCBTEST DS    0H                  FOUND JOBNAME, COMPARE IF WANTED
         CLC   JOBNAME,0(R1)       COMPARE JOBNAMES
         BNE   ASCBLOOP            TRY NEXT IF NOT THIS
         SPACE 1
ASCBSCNX DS    0H                  END OF SCAN
         BR    R4                  RETURN TO CALLER
         SPACE 1
         DROP  R3                  END CVT ADDRESSABILITY
         DROP  R7                  END ASCB ADDRESSABILITY
         TITLE 'ERROR HANDLING'
ERROR4   DS    0H                  RETURN CODE 4
         MVC   RETCODE,=H'4'       SET RETURN CODE
         B     GOODEND             GO TO EXIT
         SPACE 1
ERROR8   DS    0H                  JOBNAME NOT FOUND
         MVC   RETCODE,=H'8'       SET RETURN CODE
         B     GOODEND             GO TO EXIT
         SPACE 1
ERROR12  DS    0H                  RETURN CODE 12
         MVC   RETCODE,=H'12'      SET RETURN CODE
         B     GOODEND             GO TO EXIT
         SPACE 1
ERROR16  DS    0H                  INVALID REQUEST
         MVC   RETCODE,=H'16'      SET RETURN CODE
         B     PGMEND              GO TO EXIT
         SPACE 1
         TITLE 'DATA AREAS'
***********************************************************************
*        CONSTANTS AND WORK AREAS                                     *
***********************************************************************
         SPACE 1
         LTORG *                   LITERAL POOL
         TITLE 'DSECT MAPS'
***********************************************************************
*        HOUSEKEEPING STORAGE AREA                                    *
***********************************************************************
         SPACE 1
CLOSET   DSECT ,                   KEEP ALL OUR SKELETONS HERE
         DS    18F                 STANDARD SAVE AREA
         RIN   GENERATE            SUB-ROUTINE SAVE AREA
         SPACE 1
DWORK    DS    D'0'                DOUBLEWORD WORK AREA
RETCODE  DS    H'0'                PROGRAM RETURN CODE
JOBNAME  DS    CL8' '              AREA FOR JOBNAME
         SPACE 1
LWORK    EQU   *-CLOSET            SIZE OF THE CLOSET
         EJECT
         EJECT ,
         IRAOUCB DSECT=YES         MAP SRM USER CONTROL BLOCK
         EJECT ,
         CVT   DSECT=YES,LIST=NO   MAP COMMUNICATIONS VECTOR TABLE
         EJECT ,
         IHAPSA DSECT=YES,LIST=NO   MAP PSA
         EJECT ,
         IHAASCB DSECT=YES,LIST=NO  MAP ASCB
         END ,
